# vim: set ft=python:

###
### Exercise 5 - run tasks except for all and clean on the cluster as batch jobs
###
#
# use a cluster config file and a shell prefix for setting TMPDIR

shell.prefix("""
set -e -o pipefail;
if [[ "none$SLURM_JOBID" != none && -d /lscratch/$SLURM_JOBID ]]; then
    export TMPDIR=/lscratch/$SLURM_JOBID;
else
    mkdir -p ./tmp;
    export TMPDIR=./tmp;
fi;
""")

# can also be json
configfile: "config.yml"
HISAT_IDX = config["reference"]["hisat_index"]
SAMPLES = config["samples"].keys()

localrules: all, clean

rule all:
    input: ["04count/{}".format(a) for a in SAMPLES]

rule clean:
    shell:
        """
        rm -rf 02aln 04count
        """

rule count:    
    input: bam = "02aln/{sample}.bam",
           annot = "00ref/R64-1-1.genes.gtf"
    output: "04count/{sample}"
    log: "00log/count_{sample}"
    params: subread_ver = "1.5.1"
    threads: 2
    shell:
        """
        module load subread/{params.subread_ver}
        featureCounts -a {input.annot} -o {output} \
                -T {threads} --minOverlap 10 {input.bam}
        """

rule hisat2:
    input: fq = "00fastq/{sample}.fastq.gz",
           idx = HISAT_IDX
    output: bam = "02aln/{sample}.bam",
            bai = "02aln/{sample}.bam.bai"
    threads: 4
    log: "00log/hisat2_{sample}"
    params: hisat_ver = "2.0.5",
            samtools_ver = "1.4"
    shell:
        """
        module load hisat/{params.hisat_ver} samtools/{params.samtools_ver}
        mkdir -p 02aln
        hisat2 -x {input.idx} -U {input.fq} --threads {threads} \
          | samtools sort -T $TMPDIR/{wildcards.sample} -O BAM \
          > {output.bam}
        samtools index {output.bam}
        """
    
    
