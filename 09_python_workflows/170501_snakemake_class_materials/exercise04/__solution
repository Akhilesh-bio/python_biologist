# vim: set ft=python:

###
### Exercise 4 - using a configuration file for extracting hisat index path and sample ids
###
#
# also - plain python code in snakefiles

# can also be json
configfile: "config.yml"
HISAT_IDX = config["reference"]["hisat_index"]
SAMPLES = config["samples"].keys()

rule all:
    input: ["04count/{}".format(a) for a in SAMPLES]

# alternative way to achieve the same thing:
#rule all:
#    input: expand("04count/{sample}", sample=SAMPLES)

rule clean:
    shell:
        """
        rm -rf 02aln 04count
        """

rule count:    
    input: bam = "02aln/{sample}.bam",
           annot = "00ref/R64-1-1.genes.gtf"
    output: "04count/{sample}"
    log: "00log/count_{sample}"
    params: subread_ver = "1.5.1"
    threads: 2
    shell:
        """
        exec &> {log}
        module load subread/{params.subread_ver}
        featureCounts -a {input.annot} -o {output} \
                -T {threads} --minOverlap 10 {input.bam}
        """

rule hisat2:
    input: fq = "00fastq/{sample}.fastq.gz",
           idx = HISAT_IDX
    output: bam = "02aln/{sample}.bam",
            bai = "02aln/{sample}.bam.bai"
    threads: 4
    log: "00log/hisat2_{sample}"
    params: hisat_ver = "2.0.5",
            samtools_ver = "1.4"
    shell:
        """
        exec &> {log}
        module load hisat/{params.hisat_ver} samtools/{params.samtools_ver}
        mkdir -p 02aln
        hisat2 -x {input.idx} -U {input.fq} --threads {threads} \
          | samtools sort -T tmp/{wildcards.sample} -O BAM \
          > {output.bam}
        samtools index {output.bam}
        """
    
    
